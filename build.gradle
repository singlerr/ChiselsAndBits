plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id 'maven-publish'
	id 'com.diffplug.spotless' version '6.25.0'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	mavenLocal()
	mavenCentral()
	maven { url = "https://mvn.devos.one/releases/" }
	maven {
		name = "Fuzs Mod Resources"
		url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
	}
	maven {
		url = "https://maven.jamieswhiteshirt.com/libs-release"
		content {
			includeGroup "com.jamieswhiteshirt"
		}
	}
	maven{
		name = 'LDTTeam - Modding'
		url = 'https://ldtteam.jfrog.io/ldtteam/modding/'
	}
	maven {
		url = "https://maven.nova-committee.cn/releases"
	}
	maven {
		name = "Jitpack"
		url 'https://jitpack.io/'
	}
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
}

fabricApi {
	configureDataGeneration()
}

loom{
	accessWidenerPath = file('src/main/resources/chiselsandbits.accesswidener')
}


dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	def portingLibModules = porting_lib_modules.split(",")

	for (final def module in portingLibModules) {
		include modImplementation("io.github.fabricators_of_create.Porting-Lib:${module}:${porting_lib_version}")
	}

	include modApi("fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${config_api_version}")
	include modImplementation("com.jamieswhiteshirt:reach-entity-attributes:${reach_distance_api_version}")
	include modImplementation("com.communi-suggestu.saecularia-caudices:saecularia-caudices-fabric:${saeculariaCaudicesVersion}")
	modImplementation("maven.modrinth:modernkeybinding:1.20.4-1.3.0"){
		transitive = false
	}
	modImplementation(fileTree(dir: 'libs', includes:['*.jar']))
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}
	repositories {
	}
}

spotless {
	java{
		palantirJavaFormat()
	}

	groovyGradle {
		greclipse()
	}
}